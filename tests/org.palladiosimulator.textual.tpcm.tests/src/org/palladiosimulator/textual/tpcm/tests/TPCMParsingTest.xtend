/*
 * generated by Xtext 2.21.0
 */
package org.palladiosimulator.textual.tpcm.tests

import com.google.inject.Inject
import com.google.inject.Provider
import java.nio.file.Files
import java.nio.file.Path
import java.nio.file.Paths
import java.util.stream.Stream
import org.eclipse.core.runtime.FileLocator
import org.eclipse.core.runtime.Platform
import org.eclipse.emf.common.util.URI
import org.eclipse.xtext.resource.XtextResourceSet
import org.eclipse.xtext.testing.InjectWith
import org.eclipse.xtext.testing.extensions.InjectionExtension
import org.junit.jupiter.api.Assertions
import org.junit.jupiter.api.DynamicTest
import org.junit.jupiter.api.TestFactory
import org.junit.jupiter.api.^extension.ExtendWith
import org.junit.jupiter.api.function.Executable

import static org.junit.jupiter.api.DynamicTest.dynamicTest

@ExtendWith(InjectionExtension)
@InjectWith(TPCMInjectorProvider)
class TPCMParsingTest {
    public static val EXAMPLE_MODELS_LOCATION_PARAMETER = "examples.location"
    public static val EXAMPLE_MODELS_LOCATION_DEFAULT = "../../examples/org.palladio.textual.tpcm.examples/"
    
    @Inject
    Provider<XtextResourceSet> resourceSetProvider;

    @TestFactory
    def Stream<DynamicTest> tryParsingAllTPCMExamples() {
        val testLocation = System.properties.getOrDefault(EXAMPLE_MODELS_LOCATION_PARAMETER, EXAMPLE_MODELS_LOCATION_DEFAULT).toString()
        val testPath = Paths.get(testLocation) 
        
        var java.net.URI fileURI;
        if (Platform.isRunning) {
        	fileURI = FileLocator.toFileURL(testPath.toUri.toURL).toURI
        }   
        
        if (fileURI === null) {
            fileURI = testPath.toUri
        }
    	
        return Files.walk(Paths.get(fileURI), 1).filter([it.toString().endsWith(".tpcm")]).map([ path |
            dynamicTest("> " + path.getFileName(), path.toUri(), // test source uri
            [|testParser(path)] as Executable)
        ]);
    }

    def testParser(Path path) throws Exception {
        val rs = resourceSetProvider.get
        val r = rs.getResource(URI.createURI(path.toUri.toString), true);
        r.load(null);
        Assertions.assertEquals(r.contents.length, 1, "Unexpected number of root elements.")
        val result = r.contents.get(0)
        Assertions.assertNotNull(result)
        val errors = r.errors
        Assertions.assertTrue(errors.isEmpty, '''Unexpected errors in «path.toString»: «errors.join(", ")»''')
    }
}
